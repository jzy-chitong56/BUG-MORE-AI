#IFDEF GLOBAL
    boolean teleporting = false
    //location teleportloc = Location(0,0)
#ELSE
function TeleportJob takes unit hu returns nothing
  local group g = null
  local unit u = null
  local integer current_order = 0
  local location l = null
  call DisplayToAllJobDebug("TELEPORT JOB Start")
  if hu == null or not UnitAlive(hu) then
    set teleporting = false
    return
  endif
  set current_order = GetUnitCurrentOrder(hu)  // check teleporting
  if current_order >= 852008 and current_order <= 852013 then
    if GetItemTypeId(UnitItemInSlot(hu, current_order - 852008)) != old_id[tp_item] then
      set teleporting = false
      return
    endif
  elseif current_order != OrderId("massteleport") then
    set teleporting = false
    return
  endif
  set g = CreateGroup()
  call GroupEnumUnitsInRange(g, GetUnitX(hu), GetUnitY(hu), 2 * teleport_radius, null)
  call GroupRemoveUnit(g, hu)
  set l = GetUnitLoc(hu)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if GetOwningPlayer(u) == ai_player and not IsUnitType(u, UNIT_TYPE_PEON) and not IsUnitType(u, UNIT_TYPE_STRUCTURE) and UnitAlive(u) and not IsUnitHidden(u) and DistanceBetweenPoints_kd(l, GetUnitLoc(u)) > teleport_radius then
      call IssuePointOrderLoc(u, "move", l)
    endif
    call GroupRemoveUnit(g, u)
  endloop
  call DestroyGroup(g)
  set g = null
  call RemoveLocation(l)
  set l = null
  call TQAddUnitJob(1, TELEPORT, 0, hu)
endfunction
#ENDIF