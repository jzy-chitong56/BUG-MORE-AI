#IFDEF GLOBAL
    boolean militia_allowed = false
    boolean militia_check_enabled = true
    unit militia_hall = null
    boolean use_militia_only_on_bigger_threats = false
#ELSE
function GetMilitiaHall takes unit u returns unit
  local group g = CreateGroup()
  call GroupEnumUnitsInRangeOfLoc(g, home_location, 1200, null)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if GetUnitAbilityLevel(u, race_militia_hall_ability) > 0 and GetOwningPlayer(u) == ai_player and UnitAlive(u) and not IsUnitHidden(u) then
      call DestroyGroup(g)
      set g = null
      return u
    endif
    call GroupRemoveUnit(g,u)
  endloop
  call DestroyGroup(g)
  set g = null
  return u
endfunction

function IsMilitiaAllowed takes nothing returns boolean
  if use_militia_only_on_bigger_threats and most_threatened_town >= 0 then
    if militia_allowed then
      return town_threat[most_threatened_town] > 0
    else
      return town_threat[most_threatened_town] > 7
    endif
  endif
  return true
endfunction

function MilitiaCheckJob takes nothing returns nothing
  local group g = null
  local unit u = null
  local integer i = 0
  //local boolean big_dist = false
  local integer militiarequired = 0
  local real dist = 0
  call DisplayToAllJobDebug("MILITIA_CHECK JOB START")
  if militia_hall == null or not UnitAlive(militia_hall) then
    set militia_hall = GetMilitiaHall(militia_hall)
  endif
  if militia_hall == null then
    if race_militia_available then
      call TQAddJob(30, MILITIA_CHECK, 0)
    endif
    return
  endif
  if militia_check_enabled then
    set militia_allowed = IsMilitiaAllowed()
    set g = CreateGroup()
    call GroupEnumUnitsOfType(g, race_militia_unitstring, null)
    // set g = SelectByPlayer(g, ai_player, true)
    // set g = SelectByAlive(g, true)
    // set g = SelectByHidden(g, false)
    loop
      set u = FirstOfGroup(g)
      exitwhen u == null
      if GetOwningPlayer(u) == ai_player and UnitAlive(u) and not IsUnitHidden(u) then
        set dist = DistanceBetweenPoints_dk(GetUnitLoc(u), home_location)
        if not militia_allowed or (dist > race_militia_max_distance and dist < 2000) then  // just check home
          call IssueImmediateOrder(u, "militiaoff")
        endif
      endif
      call GroupRemoveUnit(g,u)
    endloop
    set militiarequired = Max(1,GetLocationNonCreepStrength(GetUnitX(militia_hall), GetUnitY(militia_hall), 1500) / 2) + 2 // Militia are 2 strength so divide total enemy strength by 2
    call GroupEnumUnitsOfType(g, race_militiaworker_unitstring, null)
    loop
      set u = FirstOfGroup(g)
      exitwhen u == null
      if GetOwningPlayer(u) == ai_player and UnitAlive(u) and not IsUnitHidden(u) then
        if DistanceBetweenUnits(u, militia_hall) > 1500 then  // not at home
          if GetUnitAbilityLevel(u, race_militia_ability) < 1 then
            call UnitAddAbility(u, race_militia_ability)
          endif
        else
          if militia_allowed and i > 1 and i < militiarequired then
            if GetUnitAbilityLevel(u, race_militia_ability) < 1 then
              call UnitAddAbility(u, race_militia_ability)
            endif
          elseif GetUnitAbilityLevel(u, race_militia_ability) > 0 then
            call UnitRemoveAbility(u, race_militia_ability)
          endif
          set i = i + 1
        endif
      endif
      call GroupRemoveUnit(g,u)
    endloop
    call DestroyGroup(g)
    set g = null
  endif
  //if GetUnitAbilityLevel(militia_hall, race_militia_hall_ability) > 0 then
  if race_militia_available then
    call TQAddJob(5, MILITIA_CHECK, 0)
  endif
endfunction
#ENDIF