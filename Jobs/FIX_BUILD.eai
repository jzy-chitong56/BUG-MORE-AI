#IFDEF GLOBAL
#ELSE

// ######################## Fix u #################
// #  Created by JZY 11/06/24.
// #  fix some mine to close map , AI can not u at home , but u here player home , if the player not ally , AI maybe can not u .
// ##################################################################
function FixBuildJob takes nothing returns nothing
  local group g = CreateGroup()
  local unit u = null
  local unit build = null
  local unit tempu = null
  local integer id = 0
  local real x = 0
  local real y = 0
  call DisplayToAllJobDebug("Fix Job Start")
  call GroupEnumUnitsInRangeOfLoc(g, home_location, 3000, null)
  call GroupRemoveUnit(g, expansion_peon)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if GetOwningPlayer(u) == ai_player and not IsUnitHidden(u) and IsUnitType(u, UNIT_TYPE_PEON) and not IsUnitLoaded(u) and UnitAlive(u) then
      set id = GetUnitCurrentOrder(u)
      if id == old_id[racial_hall[1]] then
        set build = u
      endif
      if not IsUnitBuying(u) and (GetUnitState(u, UNIT_STATE_LIFE) < GetUnitState(u, UNIT_STATE_MAX_LIFE) or DistanceBetweenPoints_kd(home_location,GetUnitLoc(u)) > 1360) then
        if id > 852678 then
          if (id == old_id[racial_rushcreep] and rushcreep_target != null) or id == old_id[racial_expansion] then
          else
            if GetRandomInt(0, 3) < 1 then
              set build = CreatePathingUnitFull(build, ai_player, id, GetUnitX(u) + ISign() * GetRandomReal(220, 500), GetUnitY(u) + ISign() * GetRandomReal(-500, -220))
            else
              set build = CreatePathingUnitFull(build, ai_player, id, GetLocationX(home_location) + ISign() * GetRandomReal(500, 1000), GetLocationY(home_location) + ISign() * GetRandomReal(-1000, -500))
            endif
            set x = GetUnitX(build)
            set y = GetUnitY(build)
            call RemoveUnitAM(build)
            call IssuePointOrderById(u, id, x, y)
            call CreateDebugTag("Fix Job : fix build", 10, u, 4.00, 2.00)
          endif
        elseif id != OrderId("smart") and id != OrderId("repair") and id != OrderId("renew") and id != OrderId("restoration") and id != OrderId("resumeharvesting") then
          call CreateDebugTag("Fix Job : fix harvest", 10, u, 4.00, 2.00)
          if own_town_mine[0] != null then
            call IssueTargetOrder(u, "harvest", own_town_mine[0])
          else
            call IssuePointOrder(u, "move", GetLocationX(home_location), GetLocationY(home_location))
          endif
          set tempu = u
        endif
      endif
    endif
    call GroupRemoveUnit(g, u)
  endloop
  call DestroyGroup(g)
  set g = null
  if race_uses_mine_expansion and build == null and tempu != null then
    if (first_town_mine == 1 or first_town_mine == 2) and GetGold() > 500 and TownCount(racial_hall[1]) < 2 then
      set id = old_id[racial_hall[1]]
      set build = CreatePathingUnitFull(build, ai_player, id, GetLocationX(home_location) + ISign() * GetRandomReal(600, 800), GetLocationY(home_location) + ISign() * GetRandomReal(-800, -600))
      set x = GetUnitX(build)
      set y = GetUnitY(build)
      call RemoveUnitAM(build)
      call IssuePointOrderById(tempu, id, x, y)  // help build peon
    endif
  endif
  if GetUnitCountDone(old_id[racial_farm]) < 2 then
    if id == old_id[racial_hall[1]] then
      call TQAddJob(4, FIX_BUILD, 0)  // Prevent build too much hall
    else
      call TQAddJob(2, FIX_BUILD, 0)
    endif
  elseif TownCount(racial_expansion) < 2 then
    if build != null then
      call TQAddJob(4, FIX_BUILD, 0)
    else
      call TQAddJob(30, FIX_BUILD, 0)
    endif
  endif
  set tempu = null
  set build = null
endfunction
#ENDIF